/*
   Application Goal Miners

   Gradle build file for JaCaMo Application
*/

defaultTasks 'run'

apply plugin: 'java'

version '1.0'
group   'org.jacamo'


repositories {
    mavenCentral()
    maven { url "https://raw.githubusercontent.com/jacamo-lang/mvn-repo/master" }
    maven { url "https://repo.gradle.org/gradle/libs-releases" }
    //flatDir { dirs 'lib' }
}

dependencies {
    implementation('org.jacamo:jacamo:1.2')
    implementation files('lib/search.jar')
    implementation('org.twitter4j:twitter4j-async:4.0.7')
    implementation('org.twitter4j:twitter4j-stream:4.0.7')
}

sourceSets {
    main {
        java {
            srcDir 'src/env'
            srcDir 'src/agt'
            srcDir 'src/org'
            srcDir 'src/int'
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

task run (type: JavaExec, dependsOn: 'classes') {
    description 'runs the application'
    group ' JaCaMo'
    doFirst {
        mkdir 'log'
    }
    mainClass = 'jacamo.infra.JaCaMoLauncher'
    args 'gold_miners.jcm'
    classpath sourceSets.main.runtimeClasspath
}

task buildJCMDeps (type: JavaExec, dependsOn: 'classes') {
    mainClass = 'jacamo.infra.RunJaCaMoProject'
    args = ['gold_miners.jcm', '--deps']
    classpath sourceSets.main.runtimeClasspath
}

clean {
    delete 'bin'
    delete 'build'
}
